"""
This configuration file defines the synchronization workflows between our
monorepo and the public repo.

It sets up two main workflows:
1.  A "push" workflow to export changes from the internal monorepo to the
    public repository.
2.  A "pull" or "reverse" workflow to import pull requests from the public
    repository back into the internal monorepo.
"""

load("//libs/copybara/repos", "monorepo", "monorepo_pr")
load("//libs/copybara/transformations", "insert_metadata", "remove_internal")

repo_url = "https://github.com/CyclopsBot/trashcan_skyblock_pack.git"
repo_branch = "main"

def register_workflow():
    core.workflow(
        name = "trashcans_skyblock_pack",
        origin = monorepo,
        destination = git.github_destination(
            url = repo_url,
            push = repo_branch,
        ),
        mode = "SQUASH",
        origin_files = glob(["apps/minecraft/skyblock-pack/**"], exclude = ["copy.bara.sky"]),
        destination_files = glob(["**"]),
        authoring = authoring.pass_thru(default = "Copybara <copybara@cyclopsbot.xyz>"),
        transformations =[
            # Dont leak our internal repo structure to external consumers.
            core.transform(
                transformations = [
                    core.move("apps/minecraft/skyblock-pack", ""),
                ],
                reversal = [
                    core.move("", "apps/minecraft/skyblock-pack"),
                ],
            ),
        ] + insert_metadata + remove_internal,
    )

    core.workflow(
        name = "trashcans_skyblock_pack_pr",
        origin = git.github_pr_origin(
            url = repo_url,
            branch = repo_branch,
        ),
        destination = monorepo_pr,
        mode = "CHANGE_REQUEST",
        origin_files = glob(["**"]),
        destination_files = glob(["apps/minecraft/skyblock-pack/**"]),
        authoring = authoring.pass_thru(default = "Copybara <copybara@cyclopsbot.xyz>"),
        transformations = remove_internal + [
            # Switch back to internal structure
            metadata.expose_label("COPYBARA_INTEGRATE_REVIEW"),
            metadata.save_author("ORIGINAL_AUTHOR"),
            metadata.expose_label("GITHUB_PR_NUMBER", new_name = "Closes", separator = repo_url.replace("git@github.com:", " ").replace(".git", "#")),
            core.move("", "apps/minecraft/skyblock-pack"),
        ],
    )
